apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace "com.termux.shared"
    compileSdk 34
    ndkVersion = "26.1.10909125"
    dependencies {
        implementation "androidx.appcompat:appcompat:1.6.1"
        implementation "androidx.annotation:annotation:1.7.0"
        implementation "com.google.android.material:material:1.10.0"
        // Do not bump androidx.core:core to 1.9.0, since it's gonna fail Task :app:compileDebugJavaWithJavac and Task :app:mergeDebugShaders at build
        implementation "androidx.core:core:1.12.0"
        implementation "com.google.guava:guava:32.1.3-jre"
        implementation "io.noties.markwon:core:$markwonVersion"
        implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
        implementation "io.noties.markwon:linkify:$markwonVersion"
        implementation "io.noties.markwon:recycler:$markwonVersion"
        implementation "org.lsposed.hiddenapibypass:hiddenapibypass:4.3"

        // Do not increment version higher than 1.0.0-alpha09 since it will break ViewUtils and needs to be looked into
        // noinspection GradleDependency
        implementation "androidx.window:window:1.1.0"

        // Bumping commons-io to a version higher than 2.5
        // will cause runtime exceptions on android < 8 due
        // to missing classes like java.nio.file.Path.
        implementation "commons-io:commons-io:2.15.0"

        implementation project(":terminal-view")

        implementation project(":termux-am-library")
    }

    defaultConfig {
        compileSdk 34
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        externalNativeBuild {
            ndkBuild {
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled false

        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        ndkBuild {
            path file('src/main/cpp/Android.mk')
        }
    }
}

dependencies {

}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.findByName('release')
                groupId = 'com.termux'
                artifactId = 'termux-shared'
                version = '0.118.0'
                artifact(sourceJar)
            }
        }
    }
}
